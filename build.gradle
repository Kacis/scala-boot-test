buildscript {
    repositories {
        maven {
            url "http://repo.springsource.org/libs-milestone/"
        }
        mavenCentral()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.2.3.RELEASE'
        classpath 'org.springframework:springloaded:1.2.3.RELEASE'
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}

apply plugin: 'idea'
apply plugin: 'scala'
apply plugin: 'spring-boot'
apply plugin: 'docker'

group = 'sirile'

jar {
    baseName = 'scala-spring-boot'
    version = '0.1.0'
}

def jarName = jar.baseName + '-' + jar.version + '.jar'


docker {
    baseImage = "sirile/minijavabox"
    maintainer = 'Ilkka Anttonen "ilkka.anttonen@gmail.com"'
}

task container(type: Docker, dependsOn: 'build') {
    applicationName = "scala-boot-test"
    exposePort 80
    addFile 'build/libs/' + jarName, jarName
    defaultCommand = ['java', '-Dserver.port=80', '-jar', jarName]
}

idea {
    module {
        inheritOutputDirs = false
        outputDir = file("$buildDir/classes/main/")
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.scala-lang:scala-library:2.11.6',
            'org.springframework.boot:spring-boot-starter-jetty',
            'org.springframework.boot:spring-boot-starter-actuator',
            'org.clapper:grizzled-slf4j_2.10:1.0.2'
    compile("org.springframework.boot:spring-boot-starter-web") {
        exclude module: "spring-boot-starter-tomcat"
    }
    testCompile 'org.springframework.boot:spring-boot-starter-test',
            'junit:junit'
}